# 安卓开发小工具

## APK抓取与安装

- 新建getApk.bat

  ```
  @echo off
  setlocal enabledelayedexpansion
  adb wait-for-device
  
  echo 正在获取当前焦点窗口...
  for /f "tokens=*" %%a in ('adb shell dumpsys window ^| findstr mCurrentFocus') do (
      set "currentFocusWindow=%%a"
  )
  echo Current Focus Window: !currentFocusWindow!
  
  :: 提取包名 - 修正方法
  set "line=!currentFocusWindow!"
  :: 找到包含包名的部分
  set "line=!line:*{=!"
  set "line=!line:*u0 =!"
  :: 提取包名（直到斜杠前）
  for /f "tokens=1 delims=/" %%b in ("!line!") do set "pkg=%%b"
  echo Package Name: !pkg!
  
  echo 正在获取包路径...
  for /f "tokens=*" %%c in ('adb shell pm path !pkg!') do (
      set "packagePath=%%c"
  )
  echo Package Path: !packagePath!
  
  :: 提取APK路径并拉取
  echo 正在提取APK文件...
  for /f "tokens=*" %%d in ('adb shell pm path !pkg!') do (
      set "line=%%d"
      :: 移除"package:"前缀
      set "apkPath=!line:package:=!"
      if not "!apkPath!"=="" (
          echo 拉取: !apkPath!
          adb pull "!apkPath!"
      )
  )
  
  echo 完成!
  pause
  ```

- 新建install.bat

  ```
  @echo off
  setlocal enabledelayedexpansion
  adb wait-for-device
  set "apk_files="
  
  for %%f in (*.apk) do (
  	set "apk_files=!apk_files! %%f "
  )
  
  set "apk_files=%apk_files:~0,-1%"
  
  rem echo %apk_files%
  adb install-multiple %apk_files%
  echo 完成!
  pause
  ```

